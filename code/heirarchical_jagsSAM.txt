

model{
  
  # This model assumed 4 temperature treatments an 11, 16, 21, 26. This model has three levels of heirarchy: a population level estiamte of the FR, a treatment level estimate of the FR, and an indivual level estiamte of the FR.
  
  # PRIORS
  
  # Global population estiamte
      #a
      logit_mu.a ~ dnorm(0, 1)
      mu.a <- exp(logit_mu.a)/(1+exp(logit_mu.a))

      #h
      logit_mu.h ~ dnorm(0, 1)
      mu.h <- exp(logit_mu.h)/(1+exp(logit_mu.h))

  # Treatment level effects
      for(i in 1:Ntreats){
        #a
        t.logit.a[i] ~ dnorm(mu.a, t.tau.a)
        t.a[i] <- exp(t.logit.a[i])/(1+exp(t.logit.a[i]))

        #h
        t.logit.h[i] ~ dnorm(mu.h, t.tau.h)
        t.h[i] <- exp(t.logit.h[i])/(1+exp(t.logit.h[i]))

      }

  
  # Individual level effects
      for(i in 1:num.ind){

        # a
        loga[i] ~ dnorm(t.a[t[i]], tau_int.a)
        a[i] <- exp(loga[i])/(1+exp(loga[i]))

        # h
        logh[i] ~ dnorm(t.h[t[i]], tau_int.h)
        h[i] <- exp(logh[i])/(1+exp(logh[i]))
      }
  
  #likelihood
  
      for(i in 1:n){
        
        prob[i] <- max(0.0001,min(0.9999,1/(1/a[id[i]] + h[id[i]]*initial[i])))
        
        killed[i] ~ dbin(prob[i],initial[i])
        
      }

  # Variances for all levels
      sigma_int.a ~dunif(0,10)
      tau_int.a <- 1/(sigma_int.a*sigma_int.a)
      sigma_int.h ~dunif(0,10)
      tau_int.h <- 1/(sigma_int.h*sigma_int.h)
      
      sigma_t.a ~ dunif(0,10)
      t.tau.a <- 1/(sigma_t.a*sigma_t.a)
      sigma_t.h ~ dunif(0,10)
      t.tau.h <- 1/(sigma_t.h*sigma_t.h)
  
}

